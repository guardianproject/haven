buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always', '--abbrev=0'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}



apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

repositories {
    google()
    jcenter()
    maven { url 'https://github.com/FireZenk/maven-repo/raw/master/' }
    maven { url 'https://jitpack.io' }
}

allprojects {
    project.ext {
        // these are common variables used in */build.gradle
        version_number = getVersionName()
        group_info = "haven"
        buildToolsVersion = "28.0.3"
        minSdkVersion = 16
        targetSdkVersion = 28
    }

    /**
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }**/

}


android {
    compileSdkVersion 28
    buildToolsVersion = '28.0.3'

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        javaMaxHeapSize "2g"
        preDexLibraries true
    }


    defaultConfig {
        applicationId "org.havenapp.main"
        versionCode 214
        versionName getVersionName()
        archivesBaseName = "Haven-$versionName"
        minSdkVersion 16
        targetSdkVersion 28
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

     //   renderscriptTargetApi 16
     //   renderscriptSupportModeEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // clear app state completely between tests
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0-alpha03'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.wdullaer:materialdatetimepicker:4.1.1'
    implementation 'com.github.guardianproject:signal-cli-android:v0.6.0-android-beta-1'
    implementation 'net.the4thdimension:audio-wife:1.0.3'
    implementation 'com.github.AppIntro:AppIntro:v5.1.0'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-beta1'
    implementation 'org.nanohttpd:nanohttpd-webserver:2.3.1'
    implementation 'me.angrybyte.picker:picker:1.3.1'
    implementation 'com.github.stfalcon:frescoimageviewer:0.5.0'
    implementation 'com.facebook.fresco:fresco:1.10.0'
    implementation 'com.github.derlio:audio-waveform:v1.0.1'
    implementation 'org.firezenk:audiowaves:1.1@aar'
    implementation 'com.maxproj.simplewaveform:app:1.0.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.10.4'
    implementation('com.mikepenz:aboutlibraries:6.1.1@aar') {
        transitive = true
    }

    implementation 'com.github.halilozercan:BetterVideoPlayer:1.1.0'
    implementation 'com.github.ybq:Android-SpinKit:1.2.0'
    implementation 'io.github.silvaren:easyrs:0.5.3'
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'org.jcodec:jcodec-android:0.2.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"

    testImplementation "junit:junit:4.12"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation "android.arch.persistence.room:testing:1.1.1"

    // android-job
    implementation 'com.evernote:android-job:1.2.6'

    // new camera view: https://github.com/natario1/CameraView
    implementation 'com.otaliastudios:cameraview:2.0.0-beta04'
}
