buildscript {
    ext.kotlin_version = '1.2.60'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:3.4.0-alpha01'

    }

}

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always', '--abbrev=0'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}



apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

repositories {
    google()
    jcenter()
    maven { url 'https://github.com/FireZenk/maven-repo/raw/master/' }
    maven { url 'https://jitpack.io' }
}

allprojects {
    project.ext {
        // these are common variables used in */build.gradle
        version_number = getVersionName()
        group_info = "haven"
        signal_version = "2.3.0"
        buildToolsVersion = "27.0.3"
        compileSdkVersion = 27
        minSdkVersion = 16
        targetSdkVersion = 27
        appcompat = 'com.android.support:appcompat-v7:27.1.1'
    }

}


android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        javaMaxHeapSize "1536m"
        preDexLibraries true
    }


    defaultConfig {
        applicationId "org.havenapp.main"
        versionCode 117
        versionName getVersionName()
        archivesBaseName = "Haven-$versionName"
        minSdkVersion 16
        targetSdkVersion 27
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        renderscriptTargetApi 16
        renderscriptSupportModeEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.github.guardianproject:signal-cli-android:v0.6.0-android-beta-1'
    implementation 'com.github.satyan:sugar:1.5'
    implementation 'net.the4thdimension:audio-wife:1.0.3'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    implementation 'com.nanohttpd:nanohttpd-webserver:2.2.0'
    implementation 'me.angrybyte.picker:picker:1.3.1'
    implementation 'com.github.stfalcon:frescoimageviewer:0.5.0'
    implementation 'com.facebook.fresco:fresco:1.9.0'
    implementation 'com.github.derlio.waveform:library:1.0.3@aar'
    implementation 'org.firezenk:audiowaves:1.1@aar'
    implementation 'com.maxproj.simplewaveform:app:1.0.0'
    implementation 'com.takisoft.fix:preference-v7:27.1.0.0'
    implementation 'com.wdullaer:materialdatetimepicker:3.5.2'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.8.10'
    implementation('com.mikepenz:aboutlibraries:6.0.2@aar') {
        transitive = true
    }
    implementation 'com.asksira.android:cameraviewplus:0.9.4'
    implementation 'com.github.halilozercan:BetterVideoPlayer:1.1.0'
    implementation 'io.github.silvaren:easyrs:0.5.3'
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'org.jcodec:jcodec-android:0.2.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
    androidTestImplementation "android.arch.persistence.room:testing:1.1.1"

    // firebase job dispatcher
    implementation 'com.firebase:firebase-jobdispatcher:0.8.3'
    implementation 'com.google.android.gms:play-services-base:16.0.1'
}
